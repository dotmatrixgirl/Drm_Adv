script, initChars, begin
	variable (parent, sli, i)

	addHero(char:kat)
	textbox text (3, 1)

	sli := createContainer ()
	setSliceLookup(sli, sli:chars)
	initPinBag

	parent := createContainer()
	setParent(parent, lookupslice(sli:chars))
	setSliceClipping(parent)
	resizeExtra(parent, 16)

	setStat(char:rose, stat:life, 35)
	setStat(char:rose, stat:psi, 27)
	setStat(char:rose, stat:mind, 28)
	setStat(char:rose, stat:speed, 34)
	setStat(char:rose, stat:empathy, 30)
	setStat(char:rose, stat:alignment, 60)

	updateStats(char:rose, true)

	initPinSlots(char:rose)


	setParent(loadBorderSprite(2), parent)
	$1="Rose"
	

	parent := createContainer()
	setParent(parent, lookupslice(sli:chars))
	setSliceClipping(parent)
	resizeExtra(parent, 16)

	setStat(char:kat, stat:life, 32)
	setStat(char:kat, stat:psi, 24)
	setStat(char:kat, stat:mind, 38)
	setStat(char:kat, stat:speed, 29)
	setStat(char:kat, stat:empathy, 35)
	setStat(char:kat, stat:alignment, 40)

	updateStats(char:kat, true)

	initPinSlots(char:kat)

	setParent(loadBorderSprite(1), parent)
	#$2="Kat"
	copyString(2, 3)

	equipPin(char:rose, slot:atk1, findBagPin(pin:earth))
	equipPin(char:rose, slot:atk2, findBagPin(pin:heart))
	equipPin(char:rose, slot:atr, findBagPin(pin:earthAtr))

	equipPin(char:kat, slot:atk1, findBagPin(pin:shock))
	equipPin(char:kat, slot:atr, findBagPin(pin:shockAtr))
end


script, initEnemy, who, begin
	variable (sli, i)

	sli := getStatSlice(char:enemy)
	if (sli) then (
		freeSlice (sli)
	)

	sli := createContainer()
	set parent(sli, lookupSlice(sli:chars))
	resizeExtra(sli, 16)
	setSliceClipping(sli, true)

	switch (who) do (
		case (enemy:dummy) do (
			$3="Orbweb"
			setStat(char:enemy, stat:life,	3 * getPartyLvl * 5 + 20)
			setStat(char:enemy, stat:psi,	5 * getPartyLvl * 5 / 2 + 20)
			setStat(char:enemy, stat:mind,	4  * getPartyLvl * 5 / 2 + 20)
			setStat(char:enemy, stat:speed,	3 * getPartyLvl * 5 / 2 + 20)
			setStat(char:enemy, stat:will,	3 * getPartyLvl * 5 / 2 + 20)
			setStat(char:enemy, stat:personality, type:logic)
			setStat(char:enemy, stat:mood, 5)

			initPinSlots(char:enemy)

			equipEnemyPin(slot:atk1, pin:flame, 2)
			equipEnemyPin(slot:atk2, pin:flame, 2)
			equipEnemyPin(slot:atr, pin:flameatr)

			setparent(loadBorderSprite(2), sli)
		) else (
			$3="Orbweb"
			setStat(char:enemy, stat:life,	3 * getPartyLvl * 2 + 20)
			setStat(char:enemy, stat:psi,	5 * getPartyLvl * 3 / 2 + 20)
			setStat(char:enemy, stat:mind,	4  * getPartyLvl * 3 / 2 + 20)
			setStat(char:enemy, stat:speed,	3 * getPartyLvl * 3 / 2 + 20)
			setStat(char:enemy, stat:will,	3 * getPartyLvl * 3 / 2 + 20)
			setStat(char:enemy, stat:personality, type:logic)
			setStat(char:enemy, stat:mood, 5)

			initPinSlots(char:enemy)

			equipEnemyPin(slot:atk1, pin:flame, 2)
			equipEnemyPin(slot:atk2, pin:flame, 2)
			equipEnemyPin(slot:atr, pin:flameatr)

			setparent(loadBorderSprite(2), sli)
		)
	)
	if (game:diff := diff:hard ) then (
		for (i, stat:life, stat:will) do (
			setStat (char:enemy, i, getStat(char:enemy, i) + getStat(char:enemy, i)/16)
		)
	) else if (game:diff := diff:easy) then (
		for (i, stat:life, stat:will) do (
			setStat (char:enemy, i, getStat(char:enemy, i) --  getStat(char:enemy, i)/16)
		)
	)
	return (sli)
end

script, setLvlCap, amount, begin
	if (amount > game:lvlCap) then (
		maxLvl:rose := false
		maxLvl:Kat := false
	) else (
		if (getLvl(char:rose) < getCap) then (
			maxLvl:rose := true
		)
		if (getLvl(char:kat) < getCap) then (
			maxLvl:Kat := true 
		)
	)
	game:lvlCap := amount
	
end

script, statGain, who, type, pinSlot=slot:none, modifier=0, begin
	variable(i, pin)
	if (who == char:enemy) then (
		exit script
	)
	if (modifier == 0) then (
		modifier := getLvl(char:enemy)
		modifier += 1
		modifier := modifier
	)
	if (pinslot<>slot:none) then (pin := getPinId(who, pinslot))
	
	switch (type) do (
		case (gain:damage) do (
			if (random (0, 20) <= 8) then ( 
				lvlStat(who, stat:life, modifier * (random (2, 8)) / 2)
			)
			if (random (0, 20) <= 7) then (
				lvlStat(who, stat:mind, modifier * (random (2, 8)) / 4)	
			)
		)
		case (gain:attack) do (
			if (random (0, 20) <= 10) then (
				lvlStat(who, stat:psi, modifier * (random (2, 8)) / 2)	
			)
			if (random (0, 20) <= 8) then (
				lvlStat(who, stat:speed, modifier * (random (2, 8)) / 3)
			)
			if (pinSlot<>slot:none) then (
				addTempPinExp(who, pinSlot, modifier * 4 ^ 2)
			)
			if (pin >= pin:flame && pin <= pin:earth) then (
				lvlStat(who, stat:alignment, -1)
			) else if (pin == pin:heart) then (
				lvlStat(who, stat:alignment, 1)
			)
			
		)
		case (gain:defend) do (
			if (random (0, 20) <= 7) then (
				lvlStat(who, stat:mind, modifier * (random (2, 8)) / 3)	
			)
		)
		case (gain:failtalk) do (
			if (random (0, 20) <=10) then (
				lvlStat(who, stat:empathy, modifier * (random (2, 8)) / 3)	
			)
			if (random (0, 20) <= 8) then (
				lvlStat(who, stat:mind, modifier * (random (2, 8)) / 3)
			)
			if (random (0, 20) <= 8) then (
				lvlStat(who, stat:speed, modifier * (random (2, 8)) / 3)
			)
		)
		case (gain:talked) do (
			if (random (0, 20) <= 16) then (
				lvlStat(who, stat:empathy, modifier * (random (2, 8)) / 2)	
			)
			if (random (0, 20) <= 12) then (
				lvlStat(who, stat:mind, modifier * (random (2, 8)) / 2)
			)	
			if (random (0, 20) <= 16) then (
				lvlStat(who, stat:speed, modifier * (random (2, 8)) / 3)
			)
			if (random (0, 20) <= 14) then ( 
				lvlStat(who, stat:life, modifier * (random (2, 8)) / 2)
			)
			lvlStat(who, stat:alignment, 1)
		)
		case (gain:battled) do (
			addTempPinExp(who, slot:stat, modifier * 8 ^ 2)
			if (random (0, 20) <= 8) then ( 
				lvlStat(who, stat:life, modifier * (random (2, 8)) / 2)
			)
		)
	)
end

script, getStat, who, stat, boosted=true, begin
	variable (sli, value)
	sli := getStatSlice(who)
	value := getExtra(sli, stat)
	if (boosted == true) then (
		value += getPinBoost()
	)
	return (value)

	subscript, getPinBoost, begin
		variable (pin)
		pin := getPinId(who, slot:stat)

		#add the first stat boosting pin to stat to get the stat
		if (pin -- pin:life == stat -- stat:life ) then (
			return (getPinEffect(pin, getPinLvl(who, slot:stat), value))
		)

		show value (stat)
	end
end

script, setStat, who, stat, value, begin
	variable (sli)
	sli := getStatSlice(who)

	return(setExtra(sli, stat, value))
end

script, lvlStat, who, stat, amount, capped=true, begin
	variable(i, current, lvlSum, cap, origStat, target)

	

	if (stat == stat:alignment) then (
		current := getStat(who, stat, false)
		target := current + amount
		if (target < 0) then (
			target := 0
		)
		if (target > 100) then (
			target := 100
		)
		setStat(who, stat, target)
		exitScript
	)

	current := getStat(who, stat + stat:lvled, false)
	target := current + amount

	if (capped == true) then (
		
		if (getLvl(who) < getCap) then (
			while (true) do (
				current := getStat(who, stat + stat:lvled, false)

				if (getLvl(who) < getCap) then (
					if (current < target) then (
						setStat(who, stat + stat:lvled, current + 1)
					) else (break)
				) else (
					switch(who) do (
						case (char:rose) do (maxLvl:rose := true)
						case (char:kat) do (maxLvl:kat := true)
					)
					return (stat:capped)
					break
				)
			)
		) else (
			switch(who) do (
				case (char:rose) do (maxLvl:rose := true)
				case (char:kat) do (maxLvl:kat := true)
			)
			return (stat:capped)
		)
	) else (
		setStat(who, stat + stat:lvled, target)
	)
end

script, updateStats, who, lvled=false, begin
	variable (i)

	for (i, stat:life, stat:empathy) do (
		updateStat(who, i, lvled)
	)
end

script, getCap, begin
	return (game:lvlCap)
end

script, updateStat, who, stat, lvled=false, begin
	if (lvled == false) then (
		if (getStat(who, stat + stat:lvled, false) <> getStat(who, stat, false)) then (
			return (stat:lvledUp)
		)
		setStat(who, stat, getStat(who, stat + stat:lvled, false))
	) else (
		setStat(who, stat + stat:lvled, getStat(who, stat, false))
	)
end

script, getAlign, who, begin
	if (getStat(who, stat:alignment) < 10) then (
		$11="Shattered"
	) else if (getStat(who, stat:alignment) < 20) then (
		$11="Weak"
	) else if (getStat(who, stat:alignment) < 30) then (
		$11="Poor"
	) else if (getStat(who, stat:alignment) < 40) then (
		$11="Flawed"
	) else if (getStat(who, stat:alignment) < 60) then (
		$11="Average"
	) else if (getStat(who, stat:alignment) < 70) then (
		$11="Decent"
	) else if (getStat(who, stat:alignment) < 80) then (
		$11="Strong"
	) else if (getStat(who, stat:alignment) < 90) then (
		$11="Blessed"
	) else (
		$11="Angelic"
	)
end

script, getLvl, who, begin
	variable (lvl, i)
	if (who == char:enemy) then (
		lvl += (getStat(who, stat:life, false) -- 20) / 2
	) else (
		lvl += getStat(who, stat:life, false) -- 20
	)
	for (i, stat:psi, stat:empathy) do (
		lvl += getStat(who, i, false) -- 20
	)
	lvl :=  lvl / stat:empathy / 6
	return (lvl)
end

script, getPartyLvl, begin
	variable (lvl)
	lvl := (getLvl (char:rose) + getLvl(char:Kat))/2
	return (lvl)
end

script, getStatSlice, who, begin
	variable(sli)
	sli := lookupslice(sli:chars)
	sli := sliceChild(sli, who)
	return (sli)
end

script, getCharSprite, who, begin
	variable(sli)
	sli := lookupslice(sli:chars)
	sli := sliceChild(sli, who)
	sli := sliceChild(sli, slot:atr + 1)
	return (getSpriteSetNumber(sli))
end