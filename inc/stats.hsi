script, initChars, begin
	variable (parent, sli, i)

	addHero(char:kat)
	textbox text (3, 1)

	sli := createContainer ()
	setSliceLookup(sli, sli:chars)
	sli := createContainer ()
	setSliceLookup(sli, sli:pinBag)

	parent := createContainer()
	setParent(parent, lookupslice(sli:chars))
	setSliceClipping(parent)
	resizeExtra(parent, 16)

	setStat(char:rose, stat:life, 74)
	setStat(char:rose, stat:psi, 25)
	setStat(char:rose, stat:mind, 27)
	setStat(char:rose, stat:speed, 35)
	setStat(char:rose, stat:empathy, 30)
	setStat(char:rose, stat:alignment, 55)

	updateStats(char:rose, true)

	initPinSlots(char:rose)

	equipPin(char:Rose, slot:atk1, pin:earth, 5, false)
	equipPin(char:Rose, slot:atk2, pin:heart, 5, false)
	equipPin(char:Rose, slot:atr, pin:earthatr, 0, false)

	setParent(loadBorderSprite(2), parent)
	$1="Rose"
	

	parent := createContainer()
	setParent(parent, lookupslice(sli:chars))
	setSliceClipping(parent)
	resizeExtra(parent, 16)

	setStat(char:kat, stat:life, 68)
	setStat(char:kat, stat:psi, 24)
	setStat(char:kat, stat:mind, 38)
	setStat(char:kat, stat:speed, 30)
	setStat(char:kat, stat:empathy, 35)
	setStat(char:kat, stat:alignment, 45)

	updateStats(char:kat, true)

	initPinSlots(char:kat)

	equipPin(char:kat, slot:atk1, pin:shock, 5, false)
	equipPin(char:kat, slot:atr, pin:shockatr, 0, false)

	setParent(loadBorderSprite(1), parent)
	#$2="Kat"
	copyString(2, 3)
	
end

script, initEnemy, who, begin
	variable (sli, i)

	sli := getStatSlice(char:enemy)
	if (sli) then (
		freeSlice (sli)
	)

	sli := createContainer()
	set parent(sli, lookupSlice(sli:chars))
	resizeExtra(sli, 16)
	setSliceClipping(sli, true)

	switch (who) do (
		case (enemy:dummy) do (
			$3="Orbweb"
			setStat(char:enemy, stat:life, 60)
			setStat(char:enemy, stat:psi, 30)
			setStat(char:enemy, stat:mind, 24)
			setStat(char:enemy, stat:speed, 40)
			setStat(char:enemy, stat:will, 10)
			setStat(char:enemy, stat:personality, type:logic)
			setStat(char:enemy, stat:mood, 5)

			initPinSlots(char:enemy)

			equipPin(char:enemy, slot:atk1, pin:flame, 3, false)
			equipPin(char:enemy, slot:atk2, pin:flame, 3, false)
			equipPin(char:enemy, slot:atr, pin:flameatr)

			setparent(loadBorderSprite(2), sli)
		) else (
			$3="ERROR"
			setStat(char:enemy, stat:life, 40)
			setStat(char:enemy, stat:psi, 36)
			setStat(char:enemy, stat:mind, 20)
			setStat(char:enemy, stat:speed, 24)
			setStat(char:enemy, stat:will, 10)
			setStat(char:enemy, stat:personality, type:anger)

			initPinSlots(char:enemy)

			equipPin(char:enemy, slot:atk1, pin:flame, 3, false)
			equipPin(char:enemy, slot:atk2, pin:flame, 3, false)
			equipPin(char:enemy, slot:atr, pin:flameatr)

			setparent(loadBorderSprite(2), sli)
		)
	)
	if (game:hard) then (
		for (i, stat:life, stat:will) do (
			setStat (char:enemy, i, getStat(char:enemy, i) * 10 / 8)
		)
	)
	return (sli)
end

script, setLvlCap, amount, begin
	if (amount > game:lvlCap) then (
		maxLvl:rose := false
		maxLvl:Kat := false
	) else (
		if (getLvl(char:rose) < getCap) then (
			maxLvl:rose := true
		)
		if (getLvl(char:kat) < getCap) then (
			maxLvl:Kat := true 
		)
	)
	game:lvlCap := amount
	
end

script, statGain, who, type, pinSlot=slot:none, modifier=0, begin
	variable(i, pin)
	if (who == char:enemy) then (
		exit script
	)
	if (modifier == 0) then (
		modifier := getLvl(char:enemy)
		modifier += 1
	)
	if (pinslot<>slot:none) then (pin := getPinId(who, pinslot))
	
	switch (type) do (
		case (gain:damage) do (
			if (random (0, 20) <= 4) then ( 
				lvlStat(who, stat:life, modifier * (random (2, 8)) / 4)
			)
			if (random (0, 20) <= 4) then (
				lvlStat(who, stat:mind, modifier * (random (2, 8)) / 8)	
			)
		)
		case (gain:attack) do (
			if (random (0, 20) <= 6) then (
				lvlStat(who, stat:psi, modifier * (random (2, 8)) / 4)	
			)
			if (random (0, 20) <= 3) then (
				lvlStat(who, stat:mind, modifier * (random (2, 8)) / 7)	
			)
			if (random (0, 20) <= 4) then (
				lvlStat(who, stat:speed, modifier * (random (2, 8)) / 6)
			)
			if (pinSlot<>slot:none) then (
				addPinExpCapped(who, pinSlot, modifier ^ 2 / 24)
			)
			if (pin >= pin:flame && pin <= pin:earth) then (
				lvlStat(who, stat:alignment, -1)
			) else if (pin == pin:heart) then (
				lvlStat(who, stat:alignment, 1)
			)
			
		)
		case (gain:failtalk) do (
			if (random (0, 20) <=5) then (
				lvlStat(who, stat:empathy, modifier * (random (2, 8)) / 6)	
			)
			if (random (0, 20) <= 4) then (
				lvlStat(who, stat:mind, modifier * (random (2, 8)) / 6)
			)
			if (random (0, 20) <= 4) then (
				lvlStat(who, stat:speed, modifier * (random (2, 8)) / 6)
			)
		)
		case (gain:talked) do (
			if (random (0, 20) <= 14) then (
				lvlStat(who, stat:empathy, modifier * (random (2, 8)) / 4)	
			)
			if (random (0, 20) <= 7) then (
				lvlStat(who, stat:mind, modifier * (random (2, 8)) / 5)
			)	
			if (random (0, 20) <= 4) then (
				lvlStat(who, stat:speed, modifier * (random (2, 8)) / 6)
			)
			if (random (0, 20) <= 7) then ( 
				lvlStat(who, stat:life, modifier * (random (2, 8)) / 4)
			)
			lvlStat(who, stat:alignment, 1)
		)
		case (gain:battled) do (
			for (i, slot:stat1, slot:stat2) do (
				addPinExpCapped(who, i, modifier ^ 2 / 16)
			)
			if (random (0, 20) <= 5) then (
				lvlStat(who, stat:speed, modifier * (random (2, 8)) / 6)
			)
			if (random (0, 20) <= 6) then ( 
				lvlStat(who, stat:life, modifier * (random (2, 8)) / 2)
			)
			if (random (0, 20) <= 4) then (
				lvlStat(who, stat:mind, modifier * (random (2, 8)) / 6)	
			)
		)
	)
end

script, getStat, who, stat, boosted=true, begin
	variable (sli, value)
	sli := getStatSlice(who)
	value := getExtra(sli, stat)
	if (boosted == false) then (
		return (value)
	) else (
		value += getPinBoost(slot:stat1)
		value += getPinBoost(slot:stat2)
		return (value)
	)
	subscript, getPinBoost, slot, begin
		variable (pin)
		pin := getPinId(who,slot)

		#add the first stat boosting pin to stat to get the stat
		if (pin == stat + pin:life) then (
			return (getPinEffect(pin, getPinLvl(who, 3), stat))
		)
	end
end

script, setStat, who, stat, value, begin
	variable (sli)
	sli := getStatSlice(who)

	return(setExtra(sli, stat, value))
end

script, lvlStat, who, stat, amount, capped=true, begin
	variable(i, current, lvlSum, cap, origStat, target)

	

	if (stat == stat:alignment) then (
		current := getStat(who, stat, false)
		target := current + amount
		if (target < 0) then (
			target := 0
		)
		if (target > 100) then (
			target := 100
		)
		setStat(who, stat, target)
		exitScript
	)

	current := getStat(who, stat + stat:lvled, false)
	target := current + amount

	if (capped == true) then (
		
		if (getLvl(who) < getCap) then (
			while (true) do (
				current := getStat(who, stat + stat:lvled, false)

				if (getLvl(who) < getCap) then (
					if (current < target) then (
						setStat(who, stat + stat:lvled, current + 1)
					) else (break)
				) else (
					switch(who) do (
						case (char:rose) do (maxLvl:rose := true)
						case (char:kat) do (maxLvl:kat := true)
					)
					return (stat:capped)
					break
				)
			)
		) else (
			switch(who) do (
				case (char:rose) do (maxLvl:rose := true)
				case (char:kat) do (maxLvl:kat := true)
			)
			return (stat:capped)
		)
	) else (
		setStat(who, stat + stat:lvled, target)
	)
end

script, updateStats, who, lvled=false, begin
	variable (i)

	for (i, stat:life, stat:empathy) do (
		updateStat(who, i, lvled)
	)
end

script, getCap, begin
	return (game:lvlCap)
end

script, updateStat, who, stat, lvled=false, begin
	if (lvled == false) then (
		if (getStat(who, stat + stat:lvled, false) <> getStat(who, stat, false)) then (
			return (stat:lvledUp)
		)
		setStat(who, stat, getStat(who, stat + stat:lvled, false))
	) else (
		setStat(who, stat + stat:lvled, getStat(who, stat, false))
	)
end

script, getLvl, who, begin
	variable (lvl, i)

	lvl += getStat(who, stat:life/10, false)
	for (i, stat:psi, stat:empathy) do (
		lvl += getStat(who, i, false) / 5
	)
	lvl :=  lvl / stat:empathy / 3 -- 1
	return (lvl)
end

script, getStatSlice, who, begin
	variable(sli)
	sli := lookupslice(sli:chars)
	sli := sliceChild(sli, who)
	return (sli)
end

script, getCharSprite, who, begin
	variable(sli)
	sli := lookupslice(sli:chars)
	sli := sliceChild(sli, who)
	sli := sliceChild(sli, char:sprite)
	return (getSpriteSetNumber(sli))
end