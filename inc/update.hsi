#main loop. Other scripts seprate to allow
#compiling while game is running
script, mainLoop, state,  begin
	variable(tick)
	while (true) do (
		wait()
		update(tick, state)
		tick += 1
	)
	subscript, update, tick, state, begin
		keypresses(state, tick)
		animate(state, tick)
	end
end

script, keypresses, state, tick, begin
	switch (state) do (
		case(state:resume) do (
			moveResume()
			if (keyispressed(key:shift)&&keyispressed(key:b)) then (encounter (1, tick))
			if (newpresskey(b:x)) then (menuStats())
		)
	)
	subscript, moveResume, begin
		if (heroIsWalking(me) == false) then (
			if (pressedkey(b:b)) then (
				setHeroSpeed (me, 5)
			) else (
				setHeroSpeed (me, 3)
			)

			if (pressedkey(b:u) && pressedkey(b:d)) then (exitScript())
			if (pressedkey(b:l) && pressedkey(b:r)) then (exitScript())
			if(pressedkey(b:u) && pressedkey(b:r)) then (
				if (read wall bit (heroX+1, heroY--1, southWall)==false) then (
						if(read wall bit (heroX+1, heroY--1, westWall)==false
					) then (
						setHeroSpeed(me,getHeroSpeed()--1)
						walk hero (me, up, 1), walk hero (me, right, 1)
						exit script
					)
				)
			)
			if(pressedkey(b:u) && pressedkey(b:l)) then (
				if (read wall bit (heroX--1, heroY--1, southWall)==false) then (
						if(read wall bit (heroX--1, heroY--1, eastWall)==false
					) then (
						setHeroSpeed(me,getHeroSpeed()--1)
						walk hero (me, up, 1), walk hero (me, left, 1)
						exit script
					)
				)
			)			if(pressedkey(b:d) && pressedkey(b:r)) then (
				if (read wall bit (heroX+1, heroY+1, northWall)==false) then (
						if(read wall bit (heroX+1, heroY+1, westWall)==false
					) then (
						setHeroSpeed(me,getHeroSpeed()--1)
						walk hero (me, down, 1), walk hero (me, right, 1)
						exit script
					)
				)
			)			if(pressedkey(b:d) && pressedkey(b:l)) then (
				if (read wall bit (heroX--1, heroY+1, northWall)==false) then (
						if(read wall bit (heroX--1, heroY+1, eastWall)==false
					) then (
						setHeroSpeed(me,getHeroSpeed()--1)
						walk hero (me, down, 1), walk hero (me, left, 1)
						exit script
					)	
				)
			)
			if (pressedkey(b:u)) then (
				walkHero(me, up, 1), exitScript()
			)
			if (pressedkey(b:d)) then (
				walkHero(me, down, 1), exitScript()
			)
			if (pressedkey(b:l)) then (
				walkHero(me, left, 1), exitScript()
			)
			if (pressedkey(b:r)) then (
				walkHero(me, right, 1), exitScript()
			)
			if (presskey(b:a)) then(
				if (npcAtSpot (forwardX(), forwardY())) then(
					useNpc (npcAtSpot (forwardX, forwardY))
					exitScript()
				)
			)
		)
	end
end

script, animate, state, tick, begin
	variable(sli)
	switch (state) do (
		case(state:resume) do (
			aniResume()
		)
	)
	subscript, aniResume, begin
		variable (dir, frame, i)
		
		for(i, 0, 4) do (	
			if (heroByRank(i) <>-1) then (
				dir := heroDirection(i)
				sli := lastChild(getHeroSlice(i))
				frame := getSpriteFrame(sli)
				frame := dir*4 + frame,mod,4
				if (tick,mod,6 == 0) then (
					if (frame,mod,4 == 3) then (
						frame := dir*4
					) else (
						frame := getSpriteFrame(sli) + 1
					)
				)
				setSpriteFrame(sli, frame)
			)
		)
	end
end