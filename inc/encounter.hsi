script, encounter, who, begin
	variable (parent, textSli, statSli, condition, state, turn, i, j)
	variable (target, slot, textWait, lastTurn, sel, ani, arrow)
	variable (atbRose, atbKat, atbEnemy, maxAtb, mood, question)
	initEncounter()
	while (true) do (
		if (updateEncounter()==true) then (break)
		specwait
	)
	endEncounter()

	subscript, updateEncounter, begin
		updateHP()

		if (sound is playing (0)) then (
			exitScript
		) else (
			if (condition == encounter:enraged && currentSong <> song:the storm) then (
				playSong(song:the storm)
			)
			if (condition == encounter:peaceful && currentSong <> song:the calm) then (
				playSong(song:the calm)
			)
		)
		if (textWait) then (
			if (newpresskey(b:a) || newpresskey(b:b)) then (
				textWait := false
				if (state == encounter:choose) then (
					changeState(encounter:choose)
				)
			)
			exitScript
		) else if (
			condition==encounter:won || condition==encounter:lost || condition==encounter:ran || condition==encounter:talked
		) then (
			exitReturning(true)
		)
		if (mood <= getStat(char:enemy, stat:will) * -1 && condition <> encounter:enraged) then (
			condition := encounter:enraged
			playSound(0)
			stopSong
			setSliceVisible(textSli, true)
			freeSliceChildren(textSli)
			10 $= strng:EnemyName
			$10+" became enraged."
			textLine(textSli, 0)
			textWait := true
			exitScript
		)
		
		

		if (turn == -1) then (
			setSliceVisible(textSli, false)
			if (atbRose > maxAtb) then (
				freeSliceChildren(textSli)
				turn := char:rose
				setStat(turn, stat:defending, false)
				setSliceVisible(textSli, true)
				question := random (1, questions)
				changeState(encounter:choose)
				
			) else if (atbKat > maxAtb) then (
				freeSliceChildren(textSli)
				turn := char:kat
				setStat(turn, stat:defending, false)
				setSliceVisible(textSli, true)
				question := random (1, questions)
				changeState(encounter:choose)
				
				
			) else if (atbEnemy > maxAtb) then (
				atbEnemy := char:enemy
				turn := atbEnemy
				setStat(turn, stat:defending, false)  
				setSliceVisible(textSli, true)
				freeSliceChildren(textSli)
			) else (
				if (getStat(char:rose, stat:currentlife) > 0) then (
					atbRose += getStat(char:rose, stat:speed) * 8 / 40
				) 
				if (getStat(char:kat, stat:currentlife) > 0) then (
					atbKat += getStat(char:kat, stat:speed) * 8 / 40
				)
				atbEnemy += getStat(char:enemy, stat:speed) * 8 / 40
			)
		)
		if (state == encounter:between) then (updateTurn, exitScript)
		if (state == encounter:animate) then (animations, exitScript)
		if (turn == turn:enemy) then (
			enemyTurn
		) else if (turn==char:rose || turn==char:kat) then (
			switch (state) do (
				case (encounter:choose) do (
					if (newpresskey(b:u)) then (
						if (sel<=1) then (
							sel := childCount(textSli) -- 1
						) else (sel -= 1)
					)
					if (newpresskey(b:d)) then (
						if (sel>= childCount(textSli) -- 1) then (
							sel := 1
						) else (sel += 1)
					) else if (newpresskey(b:a)) then (
						if (sel<>childCount(textSli) -- 1) then (
							if (turn==char:rose) then (
								sel:ActionRose := sel
							) else (
								sel:ActionKat := sel
							)
						)
						switch(sel) do (
							case (1) do (
								if (condition == encounter:peaceful) then (
									changeState(encounter:talk)
								) else (
									setSliceVisible(arrow, false)
									freeSliceChildren (TextSli)
									$10="They are too angry to talk!"
									textLine(textSli, 0, font:typing)
									state := encounter:choose
									textWait:= true
								)
							)
							case (2) do (
								if (getPinId(turn, slot:atk1) == pin:dummy && getPinId(turn, slot:atk2) == pin:dummy) then (
									freeSliceChildren (TextSli)
									$10="No equipped pins!"
									textLine(textSli, 0, font:typing)
									state := encounter:choose
									textWait:= true
								) else (
									changeState(encounter:skill)
								)
							)
							case (3) do (
								setSliceVisible(arrow)
								freeSliceChildren (TextSli)
								copyString(10, turn+strng:roseName)
								$10+" defends!"
								textLine(textSli, 0, font:typing)
								setStat(turn, stat:defending, true)
								textWait:= true
								state := encounter:between
							)
							case (4) do (
								changeState(encounter:run)
							)
						)
					)
				)
				case (encounter:talk) do (
					if (newpresskey(b:b)) then (
						changeState(encounter:choose)
					) else if (newpresskey(b:u)) then (
						if (sel<=1) then (
							sel := childCount(textSli) -- 1
						) else (sel -= 1)
					) else if (newpresskey(b:d)) then (
						if (sel>=childCount(textSli) -- 1) then (
							sel := 1
						) else (sel += 1)
					) else if (newpresskey(b:a)) then (
						setSliceVisible(arrow, false)
						answer(sel)
					)
				)
				case (encounter:run) do (
					if (newpresskey(b:b)) then (
						changeState(encounter:choose)
					) else if (newpresskey(b:u)) then (
						if (sel<=1) then (
							sel := childCount(textSli) -- 1
						) else (sel -= 1)
					) else if (newpresskey(b:d)) then (
						if (sel>=childCount(textSli) -- 1) then (
							sel := 1
						) else (sel += 1)
					) else if (newpresskey(b:a)) then (
						switch(sel) do (
							case (1) do (
								setSliceVisible(arrow, false)
								setSliceVisible(textSli)
								condition := encounter:ran
							)
							case (2) do (
								changeState(encounter:choose)
							)
						)
					)
				)
				case (encounter:skill) do (
					if (newpresskey(b:b)) then (
						changeState(encounter:choose)
					) else if (newpresskey(b:a)) then (
						freeSliceChildren(textSli)
						if (getPinId(turn, slot:atk1) <> pin:dummy) then (
							if (sel == 1) then (
								slot := slot:atk1
							) else if (sel == 2) then (
								slot := slot:atk2
							)
						) else (
							if (sel == 1) then (
								slot := slot:atk2
							)
						)
						if (getTargets()==target:enemy) then (
							setSliceVisible(arrow, false)
							freeSliceChildren (TextSli)
							getPinName(getPinId(turn, slot))
							if (turn == char:rose) then (
								$10="${S1} used ${S11} on "
							) else if (turn == char:kat) then (
								$10="${S2} used ${S11} on "
							)
							appendAscii(10, 10)
							$10+"${S3}."
							textline(textsli, 0, font:typing)
							target := char:enemy
							state := encounter:animate
						) else (changeState(encounter:target))					
					) else if (newpresskey(b:u)) then (
						if (sel<=1) then (
							sel := childCount(textSli) -- 1
						) else (sel -= 1)
					) else if (newpresskey(b:d)) then (
						if (sel>=childCount(textSli) -- 1) then (
							sel := 1
						) else (sel += 1)
					)
				)
				case (encounter:target) do (
					if (newpresskey(b:b)) then (
						changeState(encounter:skill)
					) else if (newpresskey(b:a)) then (
						setSliceVisible(arrow, false)
						freeSliceChildren (TextSli)
						getPinName(getPinId(turn, slot))
						if (turn == char:rose) then (
							$10="${S1} used ${S11} on "
						) else if (turn == char:kat) then (
							$10="${S2} used ${S11} on "
						)
						appendAscii(10, 10)
						if (getTargets()==target:party) then (
							switch (sel) do (
								case (1) do ($10+"${S1}.", target := char:rose)
								case (2) do ($10+"${S2}.", target := char:kat)
							)
							textLine(textSli, 0, font:typing)
							state := encounter:animate
							
						) else if (getTargets()==target:all) then (
							switch (sel) do (
								case (1) do ($10+"${S1}.", target := char:rose)
								case (2) do ($10+"${S2}.", target := char:kat)
								case (3) do ($10+"${S3}.", target := char:enemy)
							)
							textLine(textSli, 0, font:typing)
							state := encounter:animate
						)
					) else if (newpresskey(b:u)) then (
						if (sel<=1) then (
							sel := childCount(textSli) -- 1
						) else (sel -= 1)
					) else if (newpresskey(b:d)) then (
						if (sel>=childCount(textSli) -- 1) then (
							sel := 1
						) else (sel += 1)
					)
				)
			)
		)
		setSliceY(arrow, 83 + sel * 14 )
	end

	subscript, animations, begin
		variable (sli, sliAmount, textCheck)
		setSliceVisible(arrow, false)
		for (j, 0, 12) do (specwait)
		textCheck := usePin(turn, target, slot)

		setSliceVisible(textSli)
		sli := lastChild(parent)
		switch (getPinId(turn, slot)) do (
			case (pin:heart) do (
				setParent(loadHeroSprite(2), sli)
			)
			case (pin:flame) do (
				setParent(loadHeroSprite(1), sli)
			)
			else (setParent(loadheroSprite(0), sli))
		)
		sli := lastChild(sli)
		sliceToFront(sli)
		for (i, 0, 7) do (
			setSpriteFrame(sli, i)
			for (j, 0, 2) do (specwait)
		)
		freeSlice(sli)
		
		if (target == char:enemy) then (
			sli := firstChild(sliceChild(parent, 1))
		) else(
			sli := sliceChild (statSli, target)
		)
		for (j, 0, 12) do (specwait)
		if (getPinId(turn, slot) >=pin:flame && getPinId(turn, slot) <= pin:earth) then (
			for (i, 4, 0, -1) do (
				put slice (sli, -1 * i, 0)
				for (j, 0, 2) do (specwait)
				put slice (sli, i, 0)
				for (j, 0, 2) do (specwait)
			)
			put slice (parent, 0, 0)
		) else (
		)

		sliAmount := createRect ((string length(12) + 1) * 8 + 4, 16)
		setParent(sliAmount, lastChild (parent))
		textLine(sliAmount, 0, false, 12)
		switch (target) do (
			case (char:rose) do (
				#realignSlice(sliAmount, edge:left, edge:top, edge:left, edge:top)
				putSlice(sliAmount, 4, 54)
			)
			case (char:kat) do (
				realignSlice(sliAmount, edge:right, edge:top, edge:right, edge:top)
				putSlice(sliAmount, -4, 54)
			)
			case (char:enemy) do (
				centerSlice(sliAmount)
			)
		)
		specWait(24)

		freeSlice(sliAmount)
		updateHP

		if (getPinId(turn, slot) >=pin:flame && getPinId(turn, slot) <= pin:earth) then (
			if (condition <> encounter:enraged) then (
				mood := getStat(char:enemy, stat:will) * -1
			)
		)

		if (textCheck) then (
			setSliceVisible(textSli, true)
			freeSliceChildren (TextSli)
			textLine(textSli, 0, font:typing)
			textWait := true
		)
		
		state := encounter:between
	end

	subscript, updateTurn, begin
		if (turn == char:rose) then (
			atbRose := 0
		) else if (turn ==char:Kat) then (
			atbKat := 0
		)
		turn := -1
		setSliceVisible(textSli)
		state := encounter:choose

		if (getStat(char:Rose, stat:currentLife) == 0 && getStat(char:Rose, stat:currentLife) == 0) then (
			condition := encounter:lost
		)
		if (getStat(char:enemy, stat: currentLife) == 0) then (
			condition := encounter:won
		)
	end

	subscript, enemyTurn, begin
		variable (slot)
		setSliceVisible(arrow, false)
		setSliceVisible(textSli, true)
		state := encounter:choose
		if (condition == encounter:enraged) then (
			slot := random (1, 100)
			if (slot > 75) then (
				slot := slot:atk1
			) else (
				slot := slot:atk2
			)
			switch (getTargets()) do (
				case (target:enemy) do (
					target := random(char:Rose, char:Kat)	
				)
				case (target:party) do (
					target := char:enemy
				)
				case (target:all) do (
					target := random(char:Rose, char:enemy)
				)
			)
			freeSliceChildren (TextSli)

			getPinName(getPinId(turn, slot))
			$10="${S3} used ${S11} on "
			appendAscii(10, 10)
			10 $+ target + strng:roseName
			$10+"."

			textLine(textSli, 0, font:typing)
			
			state := encounter:animate
		) else if (condition == encounter:peaceful) then (
			freeSliceChildren(textSli)
			10 $= strng:EnemyName
			$10+" seems to be waiting."
			textLine(textSli, 0, font:typing)
			textWait := true
			state := encounter:between
		)
	end

	subscript, changeState, which, begin
		setSliceVisible(arrow, false)
		switch (which) do (
			case (encounter:choose) do (
				freeSliceChildren (TextSli)

				if (turn==char:rose) then (
					$10="What will ${S1} do?"
					sel := sel:ActionRose
				) else (
					$10="What will ${S2} do?"
					sel := sel:ActionKat
				)
				
				textLine(textSli, 0, font:typing)

				$10="Talk"
				textLine(textSli, 1, font:offset)
				$10="Pins"
				textLine(textSli, 2, font:offset)
				$10="Defend"
				textLine(textSli, 3, font:offset)
				$10="Run"
				textLine(textSli, 4, font:offset)
				setSliceVisible(arrow, font:typing)
			)
			case (encounter:run) do (
				freeSliceChildren(TextSli)
				$10="Run away?"
				textLine(textSli, 0, font:typing)
				$10="Yes!"
				textLine(textSli, 1, font:offset)
				$10="No..."
				textLine(textSli, 2, font:offset)
				sel := 1
				setSliceVisible(arrow, true)
			)
			case (encounter:talk) do (
				freeSliceChildren (textSli)
				initDialogue(textSli, question)
				setSliceVisible(arrow, true)
			)
			case (encounter:skill) do (
				freeSliceChildren (textSli)
				initSkill()
				setSliceVisible(arrow, true)
			) case (encounter:target) do (
				initTarget()
				setSliceVisible(arrow, true)
			)
			else (
				freeSliceChildren (textSli)
			)
		)
		state := which
	end

	subscript, responseLiked, begin
		$10="${S3} "
		switch (getStat(char:enemy, stat:personality)) do (
			case (type:logic) do (
				$10+"sees your point."
			)
			case (type:anger) do (
				$10+"seems excited."
			)
			case (type:slut) do (
				$10+"looks lustful."
			)
			case (type:selfish) do (
				$10+"appears pleased."
			)
			case (type:caring) do (
				$10="smiles."
			)
			case (type:scared) do (
				$10="calms down."
			)
			case (type:sad) do (
				$10="appears comforted."
			)
		)
	end
	subscript, responseNone, begin
		$10="${S3} "
		switch (getStat(char:enemy, stat:personality)) do (
			case (type:logic) do (
				$10+"remains silent."
			)
			case (type:anger) do (
				$10+"ignores you."
			)
			case (type:slut) do (
				$10+"appears distracted."
			)
			case (type:selfish) do (
				$10+"ignores you."
			)
			case (type:caring) do (
				$10+"seems distracted."
			)
			case (type:scared) do (
				$10+"ignores you."
			)
			case (type:sad) do (
				$10+"remains silent."
			)
		)
	end
	subscript, responseDisliked, begin
		$10="${S3} "
		switch (getStat(char:enemy, stat:personality)) do (
			case (type:logic) do (
				$10+"appears displeased."
			)
			case (type:anger) do (
				$10+"looks angry."
			)
			case (type:slut) do (
				$10+"looks angry."
			)
			case (type:selfish) do (
				$10+"looks angry."
			)
			case (type:caring) do (
				$10+"appears displeased."
			)
			case (type:scared) do (
				$10+"begins to cry."
			)
			case (type:sad) do (
				$10+"begins to cry."
			)
		)
	end

	subscript, updateHP, begin
		variable (sli)
		sli := sliceChild (statSli, 0)
		setSliceWidth(sliceChild(sli, 0), 40 * getStat(char:rose, stat:currentlife) / getStat(char:rose, stat:life))
		setSliceWidth(sliceChild(sli, 1), 40 * atbRose / maxAtb)
		sli := sliceChild (statSli, 1)
		setSliceWidth(sliceChild(sli, 0), 40 * getStat(char:kat, stat:currentlife) / getStat(char:kat, stat:life))
		setSliceWidth(sliceChild(sli, 1), 40 * atbKat / maxAtb)
	end

	subscript, initSkill, begin
		$10="What Pin will "
		10 $+ turn + strng:roseName
		$10+" use?"
		textLine(textSli, 0, font:typing)

		if (getPinId(turn, slot:atk1)<>false) then (
			getPinName(getPinId(turn, slot:atk1))
			$10="${S11} "
			if (getPinId(turn, slot:atk1) >= pin:addFlame && getPinId(turn, slot:atk1) <= pin:addEarth) then (
			) else (
				appendNumber(10, getPinLvl(turn, slot:atk1))
			)
			textLine(textSli, 1, font:offset)
		)
		if (getPinId(turn, slot:atk2)<>false) then (
			getPinName(getPinId(turn, slot:atk2))
			$10="${S11} "
			if (getPinId(turn, slot:atk2) >= pin:addFlame && getPinId(turn, slot:atk2) <= pin:addEarth) then (	
			) else (
				appendNumber(10, getPinLvl(turn, slot:atk2))
			)

			if (getPinId(turn, slot:atk1)==false) then (
				textLine(textSli, 1, font:offset)
			) else (textLine(textSli, 2, font:offset))
		)
		sel := 1
	end

	subscript, initTarget, begin
		freeSliceChildren(textSli)
		getPinName(getPinId(turn, slot)) 
		$10="Choose who to use ${S11} on"
		
		switch (getTargets()) do (
			case (target:party) do (
				textLine(textSli, 0, font:typing)
				$10="${S1}"
				expandString(10)
				textLine(textSli, 1, font:offset)
				$10="${S2}"
				expandString(10)
				textLine(textSli, 2, font:offset)
			)
			case (target:all) do (
				textLine(textSli, 0, font:typing)
				$10="${S1}"
				expandString(10)
				textLine(textSli, 1, font:offset)
				$10="${S2}"
				expandString(10)
				textLine(textSli, 2, font:offset)
				$10="${S3}"
				expandString(10)
				textLine(textSli, 3, font:offset)	
			)
			case (target:enemy) do (
				$10="Use ${S11} on ${S3}?"
				expandString(10)
				textLine(textSli, 0, font:typing)
				$10="Yes"
				textLine(textSli, 1, font:offset)
				$10="No"
				textLine(textSli, 2, font:offset)
			)
		)
		sel := 1
	end

	subscript, getTargets, begin
		if (getPinId(turn, slot) == pin:heart) then (
			return (target:party)
		) else if (getPinId(turn, slot) >= pin:addFlame && getPinId(turn, slot) <= pin:addEarth) then (
			return (target:all)
		) else (
			return (target:enemy)
		)
	end

	subscript, answer, which, begin
		
		if (sliceChild(textSli, which)==false) then (exitScript)
		switch (getExtra(sliceChild(textSli, which), getStat(char:enemy, stat:personality))) do (
			case (none) do (
				responseNone()
				freeSliceChildren(textSli)
				textLine(textSli, 0, font:typing)
				textWait := true
		 	)
		 	case (liked) do (
		 		mood += getStat(turn, stat:empathy)/30
		 		responseLiked()
		 		statGain(turn, gain:talked)
		 		freeSliceChildren(textSli)
		 		textLine(textSli, 0, font:typing)
				textWait := true
		 	)
		 	case (disliked) do (
		 		mood -= getStat(char:enemy, stat:will) / 8
		 		responseDisliked()
		 		statGain(turn, gain:failtalk)
		 		freeSliceChildren(textSli)
		 		textLine(textSli, 0, font:typing)
				textWait := true
		 	)
		)
		if (mood >= getStat(char:enemy, stat:will)) then (
			condition := encounter:talked
			10 $= strng:EnemyName
			$10+" no longer wishes to fight"
			freeSliceChildren(textSli)

			textLine(textSli, 0, font:typing)
			textWait := true
		) else if (mood <= getStat(char:enemy, stat:will) * -1) then (
		)
		state := encounter:between
	end

	subscript, initEncounter, begin
		playSong(song:the calm)
		condition := encounter:peaceful
		turn := -1
		parent := loadSliceCollection (1)
		game:state := state:encounter

		textSli := sliceChild(firstChild(parent), 1)

		arrow := sliceChild(firstChild(parent), 2)

		statSli :=  sliceChild(firstChild(parent), 3)

		who := enemy:dummy
		loadbattleback(parent, who)

		initEnemy(who)


		for (i, char:rose, char:enemy) do (
			setStat(i, stat:currentlife, getStat(i, stat:life))
			updateStats(who, true)
			setStat(who, stat:atr, getPinID(who, slot:atr) + 1 -- pin:flameAtr)
		)
		
		mood := getStat(char:enemy, stat:mood)
		maxAtb := getStat(char:rose, stat:speed) + getStat(char:kat, stat:speed) + getStat(char:enemy, stat:speed)

		maxAtb := maxAtb * 3 / 2
		updateHP()

		atbRose := random(0, maxAtb)
		atbKat := random(0, maxAtb)
		atbEnemy := random(0, maxAtb)
	end

	subscript, endEncounter, begin
		stopSong
		statGain(char:Rose, gain:battled)
		statGain(char:Kat, gain:battled)

		afterEncounter(condition, parent, who)
	end
end

script, afterEncounter, condition, parent, enemy, begin
	variable(sli, stat, exiter, textWait, who, levelIsCapped, pin, i, state)
	state := -1
	init()
	while (true) do (
		if (update) then (break)
		specwait
	)
	freeSlice(sli)

	subscript, init, begin
		if (getStat(who, stat:currentLife)<= 0) then (
			updateStats(who)
		)

		game:state := state:resume

		
		dissolve sprite (firstChild(lastChild(parent)), dissolve:fade, 45)
		specwait(44)

		freeSlice(lastChild(parent))
		freeBattleBack()
		specwait(44)
		freeSlice(parent)
		conditionInit()
	end

	subscript, update, begin
		switch(state) do (
			case (0) do (updateCapCheck)
			case (1) do (statUpdate)
			case (2) do (pinUpdate)
			else (return (true))
		)
	end

	subscript, statUpdate, begin
		if (who > char:Kat) then (
			pinInit()
		) else if (getLvl(who) >= getCap) then (
			copyString(10, who+strng:roseName)
			$10+" has reached the"
			appendAscii(10, 10)
			$10+"level cap."
			expandString(10)
			write(-1)
			who += 1
			textWait := true
		) else (
			if (updateStat(who, stat, false)) then (
				copyString(10, who+strng:RoseName)

				$10+"'s "

				concatenateStrings (10, stat + strng:life -- stat:life)

				$10+" grew to "

				appendNumber(10, getStat(who, stat))

				$10+""

				expandString(10)
				write(-1)
			) else (
				if (who > char:Kat) then (
					pinInit()
				)
			)
		)
		stat += 1
		if (stat > stat:empathy) then (
			stat := stat:life
			who += 1
		)
	end

	subscript, conditionInit, begin
		switch (condition) do (
			case (encounter:won) do (
				who := char:rose

				$10="Encounter cleared."

				write(-1)

				textWait := true
				state := 0
			)
			case (encounter:talked) do (
				who := char:rose

				10 $= strng:enemyName
				$10+" departed, satisfied"

				write(-1)

				textWait := true
				state := 0
			)
			case (encounter:ran) do (
				who := char:rose
				10 $= strng:RoseName
				$10+" and "
				10 $+ strng:KatName
				appendAscii(10, 10)
				$10+"successfully ran."
				write(-1)
				state := -1
			) else (
				state := -1
			)
		)
	end

	subscript, statinit, begin
		who := char:rose
		stat := stat:life
		state := 1
	end

	subscript, pinInit, begin
		who := char:rose
		state := 2
	end

	subscript, pinUpdate, begin
		if (who > char:Kat) then (
			state := -1
		)else if (getPinId(who, pin)) then (
			if (getPinCap(getPinLvl(who, pin))) then (
				copyString(10, who+strng:RoseName)
				getPinName(getPinId(who, pin))
				$10+"'s ${S11} pin is max level"
				expandString(10)
				write(-1)
			) else (
				if (lvlPin(who, pin)) then (
					copyString(10, who+strng:RoseName)
					getPinName(getPinId(who, pin))
					$10+"'s ${S11} pin grew to "
					appendAscii(10, 10)
					appendNumber(10, getPinLvl(who, pin))
					write(-1)
				)
			)
		)
		pin += 1
		if (pin > slot:stat2) then (
			pin := slot:atk1
			who += 1
		)
	end

	subscript, initCapCheck, begin
		state := 0
		who := char:rose
	end

	subscript,	updateCapCheck, begin
		statInit()
	end
end

define constant (5, questions)
script, initDialogue, parent, question,  begin
	variable (sli)
	switch (question) do (
		case (0) do (
			$10="question here" #put question here. always leave the "" in place
			textLine(parent, 0, font:typing) #do not change this line

			$10="answer 1 here" #replace with the first answer
			sli := textLine(parent, 1, font:offset) #do not change this line
			setExtra(sli,type:logic,	none) #change none to liked or
			setExtra(sli,type:anger,	none) #disliked to fit the
			setExtra(sli,type:slut,		none) #personality
			setExtra(sli,type:selfish,	none)
			setExtra(sli,type:caring,	none)
			setExtra(sli,type:scared,	none)
			setExtra(sli,type:sad,		none)

			$10="answer 2 here" 
			sli := textLine(parent, 2, font:offset)
			setExtra(sli,type:logic,	none)
			setExtra(sli,type:anger,	none)
			setExtra(sli,type:slut,		none)
			setExtra(sli,type:selfish,	none)
			setExtra(sli,type:caring,	none)
			setExtra(sli,type:scared,	none)
			setExtra(sli,type:sad,		none)

			$10="answer 3 here"
			sli := textLine(parent, 3, font:offset)
			setExtra(sli,type:logic,	none)
			setExtra(sli,type:anger,	none)
			setExtra(sli,type:slut,		none)
			setExtra(sli,type:selfish,	none)
			setExtra(sli,type:caring,	none)
			setExtra(sli,type:scared,	none)
			setExtra(sli,type:sad,		none)

			$10="answer 4 here"
			sli := textLine(parent, 4, font:offset)
			setExtra(sli,type:logic,	none)
			setExtra(sli,type:anger,	none)
			setExtra(sli,type:slut,		none)
			setExtra(sli,type:selfish,	none)
			setExtra(sli,type:caring,	none)
			setExtra(sli,type:scared,	none)
			setExtra(sli,type:sad,		none)
		)
		case (1) do (
			$10="What do you want from life?"
			textLine(parent, 0, font:typing)

			$10="I need nothing"
			sli := textLine(parent, 1, font:offset)
			setExtra(sli,type:logic,	liked)
			setExtra(sli,type:anger,	none)
			setExtra(sli,type:slut,		none)
			setExtra(sli,type:selfish,	none)
			setExtra(sli,type:caring,	liked)
			setExtra(sli,type:scared,	none)
			setExtra(sli,type:sad,		liked)

			$10="I want to be loved"
			sli := textLine(parent, 2, font:offset)
			setExtra(sli,type:logic,	none)
			setExtra(sli,type:anger,	disliked)
			setExtra(sli,type:slut,		none)
			setExtra(sli,type:selfish,	disliked)
			setExtra(sli,type:caring,	liked)
			setExtra(sli,type:scared,	none)
			setExtra(sli,type:sad,		none)

			$10="I want you to be happy"
			sli := textLine(parent, 3, font:offset)
			setExtra(sli,type:logic,	none)
			setExtra(sli,type:anger,	disliked)
			setExtra(sli,type:slut,		liked)
			setExtra(sli,type:selfish,	liked)
			setExtra(sli,type:caring,	liked)
			setExtra(sli,type:scared,	none)
			setExtra(sli,type:sad,		none)
		)
		case (2) do (
			$10="Do you hate me?"
			textLine(parent, 0, font:typing)

			$10="No"
			sli := textLine(parent, 1, font:offset)
			setExtra(sli,type:logic,	none)
			setExtra(sli,type:anger,	disliked)
			setExtra(sli,type:slut,		liked)
			setExtra(sli,type:selfish,	none)
			setExtra(sli,type:caring,	none)
			setExtra(sli,type:scared,	liked)
			setExtra(sli,type:sad,		liked)

			$10="Yes"
			sli := textLine(parent, 2, font:offset)
			setExtra(sli,type:logic,	none)
			setExtra(sli,type:anger,	liked)
			setExtra(sli,type:slut,		none)
			setExtra(sli,type:selfish,	disliked)
			setExtra(sli,type:caring,	none)
			setExtra(sli,type:scared,	disliked)
			setExtra(sli,type:sad,		disliked)
		)
		case (3) do (
			$10="What is love to you?" #put question here. always leave the "" in place
			textLine(parent, 0, font:typing) #do not change this line

			$10="Love is deep acceptance" #replace with the first answer
			sli := textLine(parent, 1, font:offset) #do not change this line
			setExtra(sli,type:logic,	disliked) #change none to liked or
			setExtra(sli,type:anger,	disliked) #disliked to fit the
			setExtra(sli,type:slut,		liked) #personality
			setExtra(sli,type:selfish,	none)
			setExtra(sli,type:caring,	liked)
			setExtra(sli,type:scared,	none)
			setExtra(sli,type:sad,		liked)

			$10="There is no such thing as love" 
			sli := textLine(parent, 2, font:offset) #do not change this line
			setExtra(sli,type:logic,	none)
			setExtra(sli,type:anger,	liked)
			setExtra(sli,type:slut,		disliked)
			setExtra(sli,type:selfish,	disliked)
			setExtra(sli,type:caring,	disliked)
			setExtra(sli,type:scared,	disliked)
			setExtra(sli,type:sad,		liked)

			$10="Fuck you"
			sli := textLine(parent, 3, font:offset) #do not change this line
			setExtra(sli,type:logic,	disliked)
			setExtra(sli,type:anger,	liked)
			setExtra(sli,type:slut,		disliked)
			setExtra(sli,type:selfish,	disliked)
			setExtra(sli,type:caring,	disliked)
			setExtra(sli,type:scared,	disliked)
			setExtra(sli,type:sad,		none)

			$10="Love is attraction to another"
			sli := textLine(parent, 4, font:offset) #do not change this line
			setExtra(sli,type:logic,	liked)
			setExtra(sli,type:anger,	none)
			setExtra(sli,type:slut,		none)
			setExtra(sli,type:selfish,	none)
			setExtra(sli,type:caring,	none)
			setExtra(sli,type:scared,	disliked)
			setExtra(sli,type:sad,		none)
		)
		case (4) do (
			$10="Where does life comes from?" #put question here. always leave the "" in place
			textLine(parent, 0, font:typing) #do not change this line

			$10="My cooter" #replace with the first answer
			sli := textLine(parent, 1, font:offset) #do not change this line
			setExtra(sli,type:logic,	liked) #change none to liked or
			setExtra(sli,type:anger,	none) #disliked to fit the
			setExtra(sli,type:slut,		liked) #personality
			setExtra(sli,type:selfish,	none)
			setExtra(sli,type:caring,	disliked)
			setExtra(sli,type:scared,	none)
			setExtra(sli,type:sad,		none)

			$10="It comes from the ocean" 
			sli := textLine(parent, 2, font:offset) #do not change this line
			setExtra(sli,type:logic,	liked)
			setExtra(sli,type:anger,	none)
			setExtra(sli,type:slut,		none)
			setExtra(sli,type:selfish,	none)
			setExtra(sli,type:caring,	disliked)
			setExtra(sli,type:scared,	none)
			setExtra(sli,type:sad,		liked)

			$10="They came from outer space"
			sli := textLine(parent, 3, font:offset) #do not change this line
			setExtra(sli,type:logic,	liked)
			setExtra(sli,type:anger,	none)
			setExtra(sli,type:slut,		none)
			setExtra(sli,type:selfish,	disliked)
			setExtra(sli,type:caring,	none)
			setExtra(sli,type:scared,	liked)
			setExtra(sli,type:sad,		none)

			$10="Random explosion in space"
			sli := textLine(parent, 4, font:offset) #do not change this line
			setExtra(sli,type:logic,	liked)
			setExtra(sli,type:anger,	liked)
			setExtra(sli,type:slut,		none)
			setExtra(sli,type:selfish,	none)
			setExtra(sli,type:caring,	none)
			setExtra(sli,type:scared,	disliked)
			setExtra(sli,type:sad,		none)
		) case (5) do (
			$10="Does life end after death?" #put question here. always leave the "" in place
			textLine(parent, 0, font:typing) #do not change this line

			$10="No one knows what's next" #replace with the first answer
			sli := textLine(parent, 1, font:offset) #do not change this line
			setExtra(sli,type:logic,	liked) #change none to liked or
			setExtra(sli,type:anger,	none) #disliked to fit the
			setExtra(sli,type:slut,		none) #personality
			setExtra(sli,type:selfish,	disliked)
			setExtra(sli,type:caring,	liked)
			setExtra(sli,type:scared,	disliked)
			setExtra(sli,type:sad,		disliked)

			$10="I hope so..." 
			sli := textLine(parent, 2, font:offset) #do not change this line
			setExtra(sli,type:logic,	none)
			setExtra(sli,type:anger,	none)
			setExtra(sli,type:slut,		liked)
			setExtra(sli,type:selfish,	none)
			setExtra(sli,type:caring,	liked)
			setExtra(sli,type:scared,	none)
			setExtra(sli,type:sad,		liked)

			$10="We become the universe"
			sli := textLine(parent, 3, font:offset) #do not change this line
			setExtra(sli,type:logic,	liked)
			setExtra(sli,type:anger,	none)
			setExtra(sli,type:slut,		none)
			setExtra(sli,type:selfish,	disliked)
			setExtra(sli,type:caring,	none)
			setExtra(sli,type:scared,	liked)
			setExtra(sli,type:sad,		liked)

			$10="Heaven or hell"
			sli := textLine(parent, 4, font:offset) #do not change this line
			setExtra(sli,type:logic,	disliked)
			setExtra(sli,type:anger,	none)
			setExtra(sli,type:slut,		liked)
			setExtra(sli,type:selfish,	liked)
			setExtra(sli,type:caring,	liked)
			setExtra(sli,type:scared,	disliked)
			setExtra(sli,type:sad,		none)
		)
	)
end