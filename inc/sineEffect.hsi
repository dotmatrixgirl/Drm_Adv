script, loadbattleback, parent, which, begin
	variable (sli, i, j, k, height, width)
	variable (set, blend, opacity)

	sli := load backdrop sprite (0)
	setParent (sli, parent)
	setSliceLookup(sli, sli:battleback)
	parent := lookupSlice(sli:battleback)
	sliceToBack (parent)
	height := sliceHeight(parent)
	width  := sliceWidth(parent)
	dissolve sprite (parent, dissolve:fade, 40, 0, true)
	put slice (parent, 0, -10)
	center slice (parent)

	
	#set slice clipping (sli, true)

	for (j, 0, 1) do (
		setParent(createContainer(), parent)
		sli := lastChild(parent)
		resizeExtra(sli, 16)
		if (j == 0) then (
			set		:= 18
			opacity	:= 80
			blend	:= blend:normal	
			setExtra(sli, back:rate,		100)
			setExtra(sli, back:amplitude,	2)
			setExtra(sli, back:frequency,	30)
			setExtra(sli, back:compression,	1)
			setExtra(sli, back:mode,		1)
			setExtra(sli, back:scrollx,		0)
			setExtra(sli, back:scrolly,		4)
			setExtra(sli, back:scrollxrate,	1)
			setExtra(sli, back:scrollyrate,	1)
		) else (
			set		:= 19
			opacity	:= 50
			blend	:= blend:normal
			setExtra(sli, back:rate,		80)
			setExtra(sli, back:amplitude,	3)
			setExtra(sli, back:frequency,	4)
			setExtra(sli, back:compression,	2)
			setExtra(sli, back:mode,		1)
			setExtra(sli, back:scrollx,		0)
			setExtra(sli, back:scrolly,		-12)
			setExtra(sli, back:scrollxrate,	1)
			setExtra(sli, back:scrollyrate,	1)
			
		)
		for (i, 0, height--1) do (
			sli := lastChild(parent)
			setParent (createContainer(width*2, 1), sli)
			sli := lastChild(sli)
			putSlice (sli, 0, i)
			setSliceClipping (sli, true)

			setparent (loadBackdropSprite (set), sli)
			sli := lastChild(sli)
			#setBlendMode (sli, blend)
			#setOpacity (sli, opacity)
			putSlice (sli, 0, i * -1)
			dissolveSprite (sli, dissolve:fade, 40, 0, true)
			setparent (loadBackdropSprite (set), sli)
			sli := lastChild(sli)
			#setBlendMode (sli, blend)
			#setOpacity (sli, opacity)
			putSlice (sli, width, 0)
			dissolveSprite (sli, dissolve:fade, 40, 0, true)
		)
	)
end

script, sinedist, begin
	variable (i, j, k, sli, parent, offset, x, y, t)
	variable (amplitude, frequency, rate, mode, compression)
	variable (scrolly, scrollx, height, width, scrollxrate, scrollyrate)

	parent := lookupSlice(sli:battleback)

	height := sliceHeight (parent)
	width := sliceWidth (parent)

	for (j, 0, 1) do (
		sli := sliceChild(parent, j)
		t := game:tick

		rate := getExtra(sli, back:rate)
		frequency	:= getExtra (sli, back:frequency)
		compression	:= getExtra (sli, back:compression)
		amplitude	:= getExtra (sli, back:amplitude)

		
		scrolly 	:= getExtra (sli, back:scrolly)
		scrollx 	:= getExtra (sli, back:scrollx)
		scrollxrate	:= getExtra (sli, back:scrollyrate)
		scrollyrate	:= getExtra (sli, back:scrollxrate)
		mode		:= getExtra (sli, back:mode)

		t := t*rate/10

		for (i, 0, height--1) do (
			sli := parent
			sli := sliceChild(sli, j)
			sli := sliceChild(sli, i)
			sli := firstChild(sli)

			x := get slice extra (sli, 1)
			y := get slice extra (sli, 2)

			x += scrollX
			y -= scrollY
			set slice extra (sli, 1, x)
			set slice extra (sli, 2, y)

			y := y/20
			x := x/20

			offset := sine ((frequency*i)/2 + t) * amplitude/6000

			if (mode == 1) then(
				x := offset + x
				y := i + y
			) else if (mode == 2) then (
				
				x := offset  + x
				y := i + y
				if (i, mod, 2==0) then (x := x * -1)
			) else if (mode == 3) then (
				y := i * compression + offset + Y
			)
			
			#wrap around x and y
			x += (-1 *x  / width) * width
			x := (x + width), mod, width -- width
			y += (-1 *y  / height) * height
			y := (y + height), mod, height
			
			putSlice(sli, x, y * -1)
		)
	)
end

script, freebattleback, begin
	variable (i, j, height, sli, parent)

	parent := lookupSlice(sli:battleback)
	dissolve sprite (parent, dissolve:fade, 45)
	height := sliceHeight (parent)

	for (j, 0, 1) do (
		for (i, 0, height--1) do (
			sli := sliceChild(parent, j)
			sli := sliceChild(sli,i)
			sli := firstChild(sli)
			dissolve sprite (sli, dissolve:fade, 45)
			sli := firstChild(sli)
			dissolve sprite (sli, dissolve:fade, 45)
		)
	)
end