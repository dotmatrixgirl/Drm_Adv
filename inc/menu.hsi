script, menuStats, who=0, begin
	variable (parent, sli, i, textSli)

	init()
	while (true) do (
		specwait
		if (update) then (break)
	)
	freeSlice(parent)

	subscript, update, begin
		if (newpresskey(b:b) || newpresskey(b:x)) then (
			return (true)
			exitScript()
		)
		if (newpresskey (b:u) || newpresskey (b:d) || newpresskey (b:r) || newpresskey (b:l)) then (
			who += 1
			if (who > char:kat) then (who := char:rose)
			init
		)
	end

	subscript, init, begin
		if (parent) then (freeSlice(parent))
		parent := loadSliceCollection(0)
		overlayToFront()

		sli := firstChild(firstChild(firstChild(parent)))
		setSpriteSetNumber(sli, getCharSprite(who))

		sli := firstChild(parent)
		10 $= who + 1
		sli := textLine(sli, 0)
		putSlice(sli, 30, 20)

		sli := firstChild (parent)
		sli := sliceChild(sli, 1)

		$10="Level:"
		textLine(sli)
		for (i, 30, 35) do (
			10 $= i
			$10+":"
			textLine(sli, i--29)
		)

		sli := firstChild(sli)

		$10=""
		appendNumber(10, getLvl(who))
		textLine(sli)
		for (i, stat:life, stat:alignment) do (
			$10=""
			appendNumber(10, getStat(who, i))
			textLine(sli, i -- stat:life + 1)
		)

		sli := sliceParent(sli)
		sli := nextSibling(sli)

		for (i, 36, 40) do (
			10 $= i
			$10+":"
			textLine(sli, i--36)
		)

		sli := firstChild(sli)

		for (i, slot:atk1, slot:atr) do (
			getPinName(getPinId(who, i))
			$10="${S11} "

			if (i<>slot:atr && getPinId(who, i)) then (
				appendNumber(10, getPinLvl(who, i))
			)
			textLine(sli, i -- stat:life + 1)
		)
	end
end

script, mundaneScreen, begin
	variable (sli, frame, tick)
	init()
	while (true) do (
		wait
		update
	)
	subscript, init, begin
		teleportToMap (1, 0, 0)
		setSliceVisible(getheroSlice(char:Rose), false)
		if (heroByRank(1) <> -1) then (
			setSliceVisible(getheroSlice(char:Kat), false)
		)
		sli := loadBackdropSprite (1)
		freeSlice(lookupSlice(sli:overlay))
	end
	subscript, update, begin
		if (newpresskey(b:a) || newpresskey(b:b)) then (
			resetGame
		)
		if (tick,mod,4==1) then (
			if (frame >15) then (
				frame := 1
				setSpriteSetNumber (sli, frame)
			) else (
				frame += 1
				setSpriteSetNumber (sli, frame)
			)
		)
		tick += 1
	end
end