script, menuStats, who=0, begin
	variable (parent, sli, i, textSli)

	init()
	while (true) do (
		specwait
		if (update) then (break)
	)
	freeSlice(parent)

	subscript, update, begin
		if (newpresskey(b:b) || newpresskey(b:x)) then (
			return (true)
			exitScript()
		)
		if (newpresskey (b:u) || newpresskey (b:d) || newpresskey (b:r) || newpresskey (b:l)) then (
			who += 1
			if (who > char:kat) then (who := char:rose)
			init
		)
	end

	subscript, init, begin
		if (parent) then (freeSlice(parent))
		parent := loadSliceCollection(0)
		overlayToFront()

		sli := firstChild(firstChild(firstChild(parent)))
		setSpriteSetNumber(sli, getCharSprite(who))

		sli := firstChild(parent)
		sli := firstChild(sli)
		10 $= who + strng:RoseName
		textsli := textLine(sli, 0)
		setSliceX(textSli, 20)

		$10="Align:"
		textLine(sli, 2)

		getAlign(who)
		$10="${S11}"
		textSli := textLine(sli, 2)
		setSliceX(textSli, 50)

		for (i, strng:atk1, strng:atr) do (
			10 $= i
			$10+":"
			textLine(sli, i--strng:atk1 + 4)
		)

		for (i, slot:atk1, slot:atk2) do (
			if (getPinId(who, i)) then (
				getPinName(getPinId(who, i))
				10 $= 11
				if (getPinId(who, i) >= pin:addFlame && getPinId(who, i) <= pin:addEarth) then (
				) else if (getPinId(who, i) >= pin:FlameAtr && getPinId(who, i) <= pin:EarthAtr) then (
				) else (
					$10+" "
					appendNumber(10, getPinLvl(who, i))
				)
				textSli := textLine(sli, i -- slot:atk1 + 4)
				setSliceX(textSli, 68)
			)
		)
		for (i, slot:stat1, slot:stat2) do (
			if (getPinId(who, i)) then (
				getPinName(getPinId(who, i))
				10 $= 11
				$10+" "
				appendNumber(10, getPinLvl(who, i))
				textSli := textLine(sli, i -- slot:atk1 + 4)
				setSliceX(textSli, 76)
			)
		)
		if (getPinId(who, slot:atr)) then (
			getPinName(getPinId(who, slot:atr))
			10 $= 11
			$10+" "
			textSli := textLine(sli, slot:atr -- slot:atk1 + 4)
			setSliceX(textSli, 68)
		)

		sli := firstChild (parent)
		sli := sliceChild(sli, 1)

		$10="Level:"
		textLine(sli)
		for (i, strng:life, strng:empathy) do (
			10 $= i
			$10+":"
			textLine(sli, i -- strng:life +1)
		)

		sli := firstChild(sli)

		$10=""
		appendNumber(10, getLvl(who))
		textLine(sli)

		for (i, stat:life, stat:empathy) do (
			$10=""
			appendNumber(10, getStat(who, i))
			textLine(sli, i -- stat:life + 1)
		)
	end
end

script, mundaneScreen, begin
	variable (sli, frame, tick)
	init()
	while (true) do (
		wait
		update
	)
	subscript, init, begin
		teleportToMap (1, 0, 0)
		setSliceVisible(getheroSlice(char:Rose), false)
		if (heroByRank(1) <> -1) then (
			setSliceVisible(getheroSlice(char:Kat), false)
		)
		sli := loadBackdropSprite (1)
		freeSlice(lookupSlice(sli:overlay))
	end
	subscript, update, begin
		if (newpresskey(b:a) || newpresskey(b:b)) then (
			resetGame
		)
		if (tick,mod,4==1) then (
			if (frame >15) then (
				frame := 1
				setSpriteSetNumber (sli, frame)
			) else (
				frame += 1
				setSpriteSetNumber (sli, frame)
			)
		)
		tick += 1
	end
end