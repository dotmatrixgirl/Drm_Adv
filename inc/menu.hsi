script, menuStats, who=0, begin
	variable (parent, sli, sli2, i, textSli, desc, box1, box2, arrow2, equip, graphic, scroll)
	variable (slot, bag, id, sel, sel2, scrolltemp, arrow, stats, pins)
	init()
	while (true) do (
		specwait
		if (update) then (break)
	)
	freeSlice(parent)

	subscript, update, begin
		if (equip == true) then (
			if (newpressKey(b:b) || newpresskey(b:x)) then (
				equip := false
				stopSound(sfx:cancel)
				playSound(sfx:cancel)
				init
				exitScript
			)else if (newpressKey(b:a)) then (
				stopSound(sfx:confirm)
				playSound(sfx:confirm)
				if (sel2 == childCount(sli)--1) then (
					unequipPin(who, sel+slot:atk1)
				) else (
					equipPin(who, sel+slot:atk1, sel2)
				)
				equip := false
				init
				exitScript
			) else if (pressKey(b:u)) then (
				stopSound(sfx:cursor)
				playSound(sfx:cursor)
				sli := scroll
				if (sel2 <= 0) then (
					sel2 := childCount(sli)--1
				) else (
					sel2 -= 1
				)
				scrollToSlice(scroll, sliceChild(sli, sel2))
				freeSliceChildren(desc)
				if (sel2 == childCount(sli)--1) then (
					$10="Remove equiped pin."
					setSpriteSetNumber(graphic, 0)
				) else (
					getPinDesc(getPinId(slot, sel2, true))
					10 $= 11
					setSpriteSetNumber(graphic, getPinGfx(getPinId(slot, sel2, true)))
				)
				textLine(desc)
				$10="Press [back] to return."
				textLine(desc,1)
			) else if (pressKey(b:d)) then (
				stopSound(sfx:cursor)
				playSound(sfx:cursor)
				sli := scroll
				if (sel2 >= childCount(sli)--1) then (
					sel2 := 0
				) else (
					sel2 += 1
				)
				scrollToSlice(scroll, sliceChild(sli, sel2))
				freeSliceChildren(desc)
				if (sel2 == childCount(sli)--1) then (
					$10="Remove equiped pin."
					setSpriteSetNumber(graphic, 0)
				) else (
					getPinDesc(getPinId(slot, sel2, true))
					10 $= 11
					setSpriteSetNumber(graphic, getPinGfx(getPinId(slot, sel2, true)))
				)
				textLine(desc)
				$10="Press [back] to return."
				textLine(desc,1)
			)
			putSliceScreen(arrow2, sliceScreenX(sliceChild(scroll, sel2))--2, sliceScreenY(sliceChild(scroll, sel2))+sliceHeight(sliceChild(scroll, sel2))/2)
		) else (
			if (newpresskey(b:x)) then (
				return (true)
				stopSound(sfx:cancel)
				playSound(sfx:cancel)
				exitScript()
			) else if (newpresskey(b:a)) then (
				stopSound(sfx:select)
				playSound(sfx:select)
				initEquip()
			) else if (newpresskey (b:r) || newpresskey (b:l)) then (
				stopSound(sfx:cursor)
				playSound(sfx:cursor)
				if (who == char:kat) then (who := char:rose) else (
					who := char:kat
				)
				freeSlice(parent)
				init
			) else if (pressKey(b:u)) then (
				stopSound(sfx:cursor)
				playSound(sfx:cursor)
				if (sel <= 0) then (
					sel := childCount(pins)--1
				) else (
					sel -= 1
				)
				freeSliceChildren(desc)
				if (getPinId(who, sel + slot:atk1)) then (
					getPinDesc(getPinId(who, sel + slot:atk1))
					10 $= 11
					setSpriteSetNumber(graphic, getPinGfx(getPinId(who, sel + slot:atk1)))
				) else (
					$10="No equipped pin."
					setSpriteSetNumber(graphic, 0)
				)
				
				textLine(desc)
				$10="Press [use] to change."
				textLine(desc,1)
			)
			else if (pressKey(b:d)) then (
				stopSound(sfx:cursor)
				playSound(sfx:cursor)
				if (sel >= childCount(pins)--1) then (
					sel := 0
				) else (
					sel += 1
				)
				freeSliceChildren(desc)
				if (getPinId(who, sel + slot:atk1)) then (
					getPinDesc(getPinId(who, sel + slot:atk1))
					10 $= 11
					setSpriteSetNumber(graphic, getPinGfx(getPinId(who, sel + slot:atk1)))
				) else (
					$10="No equipped pin."
					setSpriteSetNumber(graphic, 0)
				)
				textLine(desc)
				$10="Press [use] to change."
				textLine(desc,1)
			)
			putSliceScreen(arrow, sliceScreenX(sliceChild(pins, sel))--2, sliceScreenY(sliceChild(pins, sel))+sliceHeight(sliceChild(pins, sel))/2)
		)
	end

	subscript, initEquip, begin
		equip := true
		freeSliceChildren (box2)
		scroll := cloneSlice(scrolltemp)
		setSliceVisible(scroll, true)
		setParent(scroll, box2) 

		arrow2 := cloneSlice(arrow)
		sel2 := 0
		slot := sel
		bag := getBagFromSlot(slot)
		for (i, 0, childCount(getBagSlot(slot))--1) do (

			id := getPinId(slot, i, true)

			getPinName(id)
			10 $= 11

			if (slot <> slot:atr) then (
				$10+" "
				appendNumber(10, getPinLvl(who, slot, true))
			)
			sli := textLine(scroll, i, 8)
		)
		$10="Unequip"
		sli := textLine(scroll, i, 8)

		scrollToSlice(scroll, sliceChild(scroll, sel2))

		freeSliceChildren(desc)
		if (sel2 == childCount(sli)--1) then (
			$10="Remove equiped pin."
			setSpriteSetNumber(graphic, 0)
		) else (
			getPinDesc(getPinId(slot, sel2, true))
			10 $= 11
			setSpriteSetNumber(graphic, getPinGfx(getPinId(slot, sel2, true)))
		)
		textLine(desc)
		$10="Press [back] to return."
		textLine(desc,1)

		putSliceScreen(arrow2, sliceScreenX(sliceChild(scroll, sel2))--2, sliceScreenY(sliceChild(scroll, sel2))+sliceHeight(sliceChild(scroll, sel2))/2)
	end

	subscript, init, begin
		if (parent) then (freeSlice(parent))
		parent := loadSliceCollection(0)
		overlayToFront()

		box1	:= sliceChild(parent, 0)
		box2	:= sliceChild(parent, 1)
		desc	:= sliceChild(parent, 2)
		graphic	:= firstChild(sliceChild(parent, 3))
		arrow	:= sliceChild(parent, 4)
		pins	:= lastChild(box1)
		scrolltemp	:= sliceChild(parent, 5)

		sli := firstChild(box1)
		setSpriteSetNumber(sli, getCharSprite(who))

		10 $= who + strng:RoseName
		textsli := textLine(box1, 0, 24)
		$10="Align: "
		getAlign(who)
		$10+"${S11}"
		textsli := textLine(box1, 1, 24)

		for (i, strng:atk1, strng:atr) do (
			10 $= i
			$10+" : "
			slot := (i -- strng:atk1 + slot:atk1)
	
			if (getPinId(who, slot)) then (
				getPinName(getPinId(who, slot))
				10 $+ 11
				if (slot <> slot:atr) then (
					$10+" "
					appendNumber(10, getPinLvl(who, slot))
				)
			)
			textLine(pins, i--strng:atk1 + 3, 8)
		)

		sli := box2
		$10="Level "
		appendNumber(10, getLvl(who))
		textLine(box2)

		for (i, stat:life, stat:empathy) do (
			10 $= i -- stat:life + strng:life
			$10+":"
			sli2 := textLine(box2, i -- stat:life + 2)
			$10=""
			appendNumber(10, getStat(who, i))
			textSli := textLine(box2, i-- stat:life + 2, 60)
		)
		freeSliceChildren(desc)
		if (getPinId(who, sel + slot:atk1)) then (
			getPinDesc(getPinId(who, sel + slot:atk1))
			10 $= 11
			setSpriteSetNumber(graphic, getPinGfx(getPinId(who, sel + slot:atk1)))
		) else (
			$10="No equipped pin."
			setSpriteSetNumber(graphic, 0)
		)
		textLine(desc)
		$10="Press [use] to change."
		textLine(desc,1)
		putSliceScreen(arrow, sliceScreenX(sliceChild(pins, sel))--2, sliceScreenY(sliceChild(pins, sel))+sliceHeight(sliceChild(pins, sel))/2)
	end
end

script, mundaneScreen, begin
	variable (sli, frame, tick)
	init()
	while (true) do (
		wait
		update
	)
	subscript, init, begin
		game:state := state:suspend
		teleportToMap (1, 0, 0)
		setSliceVisible(getheroSlice(char:Rose), false)
		if (heroByRank(1) <> -1) then (
			setSliceVisible(getheroSlice(char:Kat), false)
		)
		sli := loadBackdropSprite (1)
		freeSlice(lookupSlice(sli:overlay))
	end
	subscript, update, begin
		if (newpresskey(b:a) || newpresskey(b:b)) then (
			resetGame
		)
		if (tick,mod,4==1) then (
			if (frame >15) then (
				frame := 1
				setSpriteSetNumber (sli, frame)
			) else (
				frame += 1
				setSpriteSetNumber (sli, frame)
			)
		)
		tick += 1
	end
end